buildscript {
    repositories {
        jcenter()
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
}

plugins {
    id 'scala'
    id 'org.spongepowered.plugin' version '0.8.1'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

projects {
    version '2.0.0-alpha.1'
    group 'io.izzel.amberadvancedattributes'
    sponge.plugin.id = 'amberadvancedattributes'

    defaultTasks 'clean', 'build'
    tasks.build.dependsOn tasks.shadowJar
}

sourceSets {
    api
    main {
        compileClasspath += api.output
    }
}

configurations {
    compilerPlugin
}

repositories {
    mavenCentral()
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
}

dependencies {
    compile 'org.scala-lang:scala-library:2.11.1'
    compile 'org.scala-lang:scala-reflect:2.11.1'
    compile 'org.spongepowered:spongeapi:7.2.0-SNAPSHOT'
    apiCompile 'org.spongepowered:spongeapi:7.2.0-SNAPSHOT'
    compile 'com.github.IzzelAliz:AmberCommon:master-SNAPSHOT'
    compile 'com.github.randombyte-developer:byte-items:v2.4.1'
    compile 'org.scala-lang.plugins:scala-continuations-library_2.11:1.0.2'
    compilerPlugin 'org.scala-lang.plugins:scala-continuations-plugin_2.11.1:1.0.2'
}

compileScala {
    sourceCompatibility = targetCompatibility = '1.7'
    scalaCompileOptions.additionalParameters = ['-Xplugin:' + configurations.compilerPlugin.asPath, '-P:continuations:enable']
}

shadowJar {
    minimize()
    archiveClassifier.set(null)
    dependencies {
        it.include it.dependency('com.github.IzzelAliz:.*')
    }
    from sourceSets.api.output
}

jar {
    archiveClassifier.set('pure')
    from sourceSets.api.output
}

task apiJar(type: Jar) {
    archiveClassifier.set('api')
    from sourceSets.api.output
}

jar.dependsOn(apiJar)